OPTION EXPLICIT

// --------------------------------------------------------------
// エクスプローラを複数開いて並べるスクリプト
// 「設定部分」と「実行部分」を任意に変更したうえで動かす。
// --------------------------------------------------------------

// ---- 設定部分start -------------------------------------------

// Win+Eのあと、次の動作までの待ち時間。
// PC性能によっては、エクスプローラの起動に時間がかかるため、
// 間に合わずに次の動作に入るとうまくいかない。
// 高性能なPCなら0でも動くかもしれない。
PUBLIC wait_after_win_e = 1

// 個々の動作の間で挟む待ち時間。
// PC性能によっては、待ちを入れないとうまく動いてくれない。
// 高性能なPCなら0でも動くかもしれない。
PUBLIC wait_petit = 0.3


// 並べるウィンドウの位置と幅に関する定義。
DIM x = 0 // 一番左上に配置するウィンドウのx座標
DIM y = 0 // 一番左上に配置するウィンドウのy座標
PUBLIC width = 700 // エクスプローラのウィンドウ幅
PUBLIC height = 350 // エクスプローラのウィンドウ高さ

// ---- 設定部分end ---------------------------------------------
// ---- 設定部分start -------------------------------------------

// 配置順と位置とパスは任意に
execExplorer(0, 0, "c:\")
execExplorer(1, 0, "B:\_usr\_temp\rec\screen_shot")
execExplorer(0, 1, "C:\dev\src\github")
execExplorer(1, 1, "C:\dev\src\github")
execExplorer(0, 2, "B:\_usr\_temp\rec\screen_shot")
execExplorer(1, 2, "C:\_usr\app\_no_inst\uwsc\script")

// ---- 実行部分end ---------------------------------------------
EXITEXIT

// ----------------------------------------
// エクスプローラを起動する
// @gridX グリッド位置_x方向
// @gridY グリッド位置_y方向
// @path: エクスプローラに開かせるフォルダパス
// ----------------------------------------
FUNCTION execExplorer(gridX, gridY, path)
	
	// explorer起動。
	// MEMO: EXECでやった場合、
	//       複数のエクスプローラの識別が怪しくなって
	//       うまく動いてくれない。
	//       仕方ないのでWin+Eで起動している。
	KBD(VK_RWIN,DOWN,0)
	KBD(VK_E,CLICK,0)
	KBD(VK_RWIN,UP,0)
	SLEEP(wait_after_win_e)
	
	
	DIM offsetX = gridX * width
	DIM offsetY = gridY * height

	// ウィンドウ位置設定
	ACW(GETID(GET_ACTIVE_WIN), offsetX, offsetY, width, height)
	
	// フォルダ移動
	SLEEP(wait_petit)
	KBD(VK_RALT,DOWN,0)
	KBD(VK_D,CLICK,0)
	KBD(VK_RALT,UP,0)
	SLEEP(wait_petit)
	KBD(VK_RCTRL,DOWN,0)
	KBD(VK_A,CLICK,0)
	KBD(VK_RCTRL,UP,0)
	SLEEP(wait_petit)
	KBD(VK_DELETE,CLICK,0)
	SENDSTR(GETID(GET_ACTIVE_WIN), path)
	KBD(VK_RETURN,CLICK,0)
	
	RESULT = 0
	FEND

